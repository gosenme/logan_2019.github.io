---
title: 接口自动化测试实战-12
---
<article id="topicContainer" class="column_content"><h2 class="topic_title"></h2><div><p>在前面的课程中我们讲解了 JSON 格式接口 Request 的管理和 Response 的解析校验，本课程将带领大家学习 content-type 为 XML 的接口如何完成自动化测试。</p>
<p>首先讲解 XML 格式接口的 Request Body 管理，实际 velocity 可以作为 JSON 格式数据的模板外还可以处理 XML 格式的数据，使用方式跟前面相同，所以这里我们只给出一个学习的demo。</p>
<p>还是相同的配方，先通过 WireMock 模拟被测接口。</p>
<p>接口地址：http://localhost:9090/api/addXmlData/xml，WireMock 的 mapping 文件如下：</p>
<pre><code>{
  "request": {
    "method": "POST",
    "urlPathPattern": "/api/addXmlData/([a-z]*)",
    "headers": {
      "Content-Type": {
        "equalTo": "application/xml;charset=utf-8"
      }
    },
    "bodyPatterns": [
      {
        "matchesXPath": {
          "expression": "/bookstore/book/author/text()",
          "contains": "author"
        },
        "matchesXpath":{
          "expression":"/bookstore/book/price/text()",
          "contains": "2"
        },
        "matchesXPath": {
          "expression": "/bookstore/book/bookName/text()",
          "contains": "bookName"
        }
      }
    ]
  },
  "response": {
    "status": 200,
    "body": "add xml data successfully"
  }
}
</code></pre>
<p>根据上面的 mapping 文件，接口的 Request Body 需要输入如下内容</p>
<pre><code>&lt;bookstore&gt;
    &lt;book&gt;
        &lt;bookName&gt;bookName Harry Potter&lt;/bookName&gt;
        &lt;price&gt;26&lt;/price&gt;
        &lt;author&gt;author-name&lt;/author&gt;
    &lt;/book&gt;
&lt;/bookstore&gt;
</code></pre>
<p>为了构造上述的 Body 体，创建模板文件 addXmlData.vm（模版文件即可以是 JSON 文件也可以是 vm 文件）和 XmlTemplateService，模板文件内容如下：</p>
<pre><code>&lt;bookstore&gt;
    &lt;book&gt;
        &lt;bookName&gt;${addXmlData.bookName}&lt;/bookName&gt;
        &lt;price&gt;${addXmlData.price}&lt;/price&gt;
        &lt;author&gt;${addXmlData.author}&lt;/author&gt;
    &lt;/book&gt;
&lt;/bookstore&gt;
</code></pre>
<p>XmlTemplateService 的代码如下，与生成 JSON 格式的处理一致</p>
<pre><code>class XmlTemplateService {
    VelocityEngine velocityEngine = new VelocityEngine()
    VelocityContext velocityContext = new VelocityContext()
    StringWriter stringWriter = new StringWriter()

    def getAddXmlDataBody(addXmlData) {
        velocityContext.put("addXmlData", addXmlData)
        velocityEngine.getTemplate("src/test/resources/com/github/body/template/addXmlData.vm").merge(velocityContext, stringWriter)   //这里写入自己创建的vm文件地址
        stringWriter.toString()
    }
}
</code></pre>
<p>定义 AddXmlDataBody，通过构造模式构建需要的 Request Body</p>
<pre><code>//使用方式和 JSON格式的接口相同，所以这里不做更多的代码解析
class AddXmlDataBody {
    def bookName
    def price
    def author
    XmlTemplateService xmlTemplateService

    AddXmlDataBody() {
        xmlTemplateService = new XmlTemplateService()
    }

    def setBookName(bookName) {
        this.bookName = bookName
        this
    }
    def setPrice(price){
        this.price=price
        this
    }

    def setAuthor(author){
        this.author=author
        this
    }

    def getAddXmlDataBody() {
        xmlTemplateService.getAddXmlDataBody(this)
    }
}
</code></pre>
<p>创建 XmlClient，添加 addXmlData method</p>
<pre><code> def addXmlData(reqBody,expectCode=200) {

        def res = given().baseUri("http://localhost:9090")
                .header("Content-Type", "application/xml;charset=utf-8")     //设置接口的content-type为“application/xml”
                .body(reqBody)
                .when()
                .post("/api/addXmlData/xml")
                .then().assertThat().statusCode(expectCode)
                .extract().response().getBody().asString()        //获取response body
        res

    }
</code></pre>
<p>上述内容创建完后就可以编写 Case 校验整个过程是否正确，AddXmlDataCas 中包含两个场景，第一个是正向场景，第二个是异常场景（输入无效 bookName 时调用接口返回 404 错误码)，代码如下：</p>
<pre><code>package com.github.sevenCourse

import org.junit.Assert
import spock.lang.Specification
class AddXmlDataCase extends Specification {
    XmlTemplateService xmlTemplateService
    XmlClient xmlClient

    def setup() {
        xmlTemplateService = new XmlTemplateService()
        xmlClient = new XmlClient()
    }

    def "should add xml data successfully"() {
        given: "generate add xml data api request body"
        def reqBody = new AddXmlDataBody()
                .setBookName("bookName-shanguo")
                .setPrice(20)
                .setAuthor("luoguanzhong")
                .getAddXmlDataBody()

        when: "call add xml data api"
        def res = xmlClient.addXmlData(reqBody)
        then: "should get correct response"
        Assert.assertEquals(res, "add xml data successfully")     //校验接口返回的response body是否正确
    }

    def "should add xml data with invalid bookName failed"() {
        given: "generate add xml data api request body"
        def reqBody = new AddXmlDataBody()
                .setBookName("shanguo")       //mock接口的时候要求request body中bookName字段必须包含“bookName”字符串，这里设置的书名不正确，调用接口应该会失败
                .setPrice(20)
                .setAuthor("luoguanzhong")
                .getAddXmlDataBody()

        when: "call add xml data api"
        xmlClient.addXmlData(reqBody, 404)   //校验接口返回404状态码
        then: "no then"
    }
}
</code></pre>
<p>运行上面的 Case 如下图所示，可以看到全部运行成功。</p>
<p><img src="https://images.gitbook.cn/15749365131215" alt="" /></p>
<p>上面练习了 POST 请求的 XML 格式的接口，接着我们测试 GET 请求接口，重点在解析校验 XML 格式的 Request Body 上。还是相同的配方，启动 WireMock 服务，Mock一个GET 的接口，调用接口期望返回如下 Response。
接口url：<a href="http://localhost:9090/api/getXmlData">http://localhost:9090/api/getXmlData</a></p>
<pre><code>&lt;Response&gt;
    &lt;MetaInfo&gt;
        &lt;Timestamp&gt;2015-09-15T06:10:48.562Z&lt;/Timestamp&gt;
    &lt;/MetaInfo&gt;
    &lt;View type="ns2:SearchResultsViewType"&gt;
        &lt;ViewId&gt;0&lt;/ViewId&gt;
        &lt;Result&gt;
            &lt;Relevance&gt;1.0&lt;/Relevance&gt;
            &lt;MatchLevel&gt;houseNumber&lt;/MatchLevel&gt;
            &lt;Location&gt;
                &lt;MapView&gt;
                    &lt;TopLeft&gt;
                        &lt;Latitude&gt;41.88&lt;/Latitude&gt;
                        &lt;Longitude&gt;-87.64&lt;/Longitude&gt;
                    &lt;/TopLeft&gt;
                    &lt;TopLeft&gt;
                        &lt;Latitude&gt;41.90&lt;/Latitude&gt;
                        &lt;Longitude&gt;-87.60&lt;/Longitude&gt;
                    &lt;/TopLeft&gt;
                &lt;/MapView&gt;
                &lt;Address&gt;
                    &lt;Country&gt;USA&lt;/Country&gt;
                    &lt;State&gt;IL&lt;/State&gt;
                    &lt;County&gt;Cook&lt;/County&gt;
                    &lt;City&gt;Chicago&lt;/City&gt;
                    &lt;District&gt;West Loop&lt;/District&gt;
                    &lt;Street&gt;W Randolph St&lt;/Street&gt;
                    &lt;HouseNumber&gt;425&lt;/HouseNumber&gt;
                    &lt;PostalCode&gt;60606&lt;/PostalCode&gt;
                    &lt;AdditionalData key="CountryName"&gt;United States&lt;/AdditionalData&gt;
                    &lt;AdditionalData key="StateName"&gt;Illinois&lt;/AdditionalData&gt;
                    &lt;AdditionalData key="CountyName"&gt;Cook&lt;/AdditionalData&gt;
                    &lt;AdditionalData key="PostalCodeType"&gt;N&lt;/AdditionalData&gt;
                &lt;/Address&gt;
            &lt;/Location&gt;
        &lt;/Result&gt;
    &lt;/View&gt;
&lt;/Response&gt;
</code></pre>
<p>假设三个小的测试场景</p>
<ol>
<li>打印下值。</li>
<li>打印下所有类型</li>
<li>查找中<code>key=CountryName</code>的字段，并打印对应的 value 信息</li>
</ol>
<p>为了完成这三个case，创建XmlClient和XmlDataService。</p>
<ul>
<li>XmlClient 中使用 REST Assrued 调用接口，获取 response body，这个部分和前面 JSON 格式的接口无区别。</li>
<li>XmlDataService 负责数据的解析校验</li>
</ul>
<p>以下 Case 采用"获取 Body 内容，然后转化为 string 类型"的处理方式</p>
<pre><code>class XmlClient {
    def getXmlData() {
        def res = given().baseUri("http://localhost:9090")
                .when()
                .get("/api/getXmlData")
                .then().assertThat().statusCode(200)
                .extract().response().getBody().asString()    //获取接口的response body
        res
    }
}
</code></pre>
<p>XmlDataService 负责 Request Body 的解析和校验，为了方便验证解析的内容是否正确，我直接在 XmlDataService 中采用 junit 提供的 @Test 注解执行代码。</p>
<p>以下是打印 MatchLevel 值的代码，可以看到 xmlSlurper 和 jsonSlurper 的使用方式很像，都是调用 parseText 方法</p>
<pre><code>class XmlDataService {
    XmlSlurper xmlSlurper
    XmlClient xmlClient

    XmlDataService() {
        xmlSlurper = new XmlSlurper()
        xmlClient = new XmlClient()
    }

    @Test()
    void getMatchLevel() {
        def result = xmlSlurper.parseText(xmlClient.getXmlData())   //将string类型的respone body通过xmlSlurper转换为数据对象
        println result.View.Result.MatchLevel.text()      //打印数据对象中的MatchLevel值
    }
}
</code></pre>
<p>需要注意的点是： JSON 格式的数据通过 <code>jsonSlurper.parseText</code>处理后返回一个map 对象，而 XML 格式的数据通过<code>xmlSlurper.parseText</code>处理后返回的是一个 Node 对象，Node 对象结构如下图所示。</p>
<p>可以看到该node对象的名字是 Response，第一个子节点是 View，如果要获取MatchLevel 的值，写入<code>result.View.Result.MatchLevel.text()</code>即可。</p>
<p><img src="https://images.gitbook.cn/15749365131237" alt="" /></p>
<p>接着我们完成第二个 Case，打印 MapView 下面所有节点的值。采用 each{} 打印所有 TopLeft 节点下子节点的值，另外 Case 中还使用了 find 方法查找 TopLeft 下Ltitude 值等于 41.88 的 TopLeft 节点，然后打印该节点下子节点的值。</p>
<pre><code>  @Test()
    void getMapView() {
        def result = xmlSlurper.parseText(xmlClient.getXmlData())
         result.View.Result.Location.MapView.TopLeft.each { it -&gt; println it }   // 通过each进行遍历
        println result.View.Result.Location.MapView.TopLeft.find { it -&gt; it.Latitude == 41.88 }.Longitude   //通过find查找符合条件的内容

    }
</code></pre>
<p>执行以上 case，能得到如下结果则说明 case 编写正确</p>
<pre><code>41.88-87.64
41.90-87.60
-87.64
</code></pre>
<p>除了根据节点 value 进行筛选外，我们还可以根据节点的属性值进行筛选，第三个case是<code>查找中key=CountryName</code>的节点。</p>
<p>针对第三个测试场景可以使用<code>find{}</code>查找所有 AdditionalData 节点中 key 属性的值等于“CountryName”的节点，然后打印该节点的值。</p>
<p>代码如下所示：</p>
<pre><code> @Test()
    void getAddress() {
        def result = xmlSlurper.parseText(xmlClient.getXmlData())
        println result.View.Result.Location.Address.AdditionalData.find { it -&gt; it.@'key' == "CountryName" }.text()
    }
</code></pre>
<p>如下是运行上面三个测试场景视频，如果在学习中遇到困难可以参考视频进行练习。</p>
<p><img src="https://images.gitbook.cn/15749365131255" alt="" /></p>
<p>至此本次课程就讲解完成了，此次课程主要带领大家学习了如何通过xmlSlurper解析和校验 XML 格式接口的 Request Body，思路和 JSON 格式的接口几乎相同。实际 REST Assrued 还提供了 xpath 方式获取 Request Body 内容，xpath 和 jsonpath 的使用方式很像，大家可以自行练习 xpath 的使用。</p>
<p>REST Assrued 官网学习手册地址：https://github.com/rest-assured/rest-assured/wiki/Usage</p>
<p>下章节将带领大家学习接口测试配置信息、测试数据管理的内容。</p></div></article>