---
title: 从 0 开始搭建 IoT 平台-42
---
<article id="topicContainer" class="column_content"><h2 class="topic_title"></h2><div><p>这一课，我们继续完成处理其他事件的代码，但是因为本课程的篇幅有限，这里只完成 IotHub 目前需要的 "client.disconnected" 和 "message.publish" 事件的处理代码，其他事件的处理很简单，只需要依葫芦画瓢就可以了。有需要的话，大家可以自行进行扩展。</p>
<h3 id="clientdisconnected">处理 "client.disconnected" 事件</h3>
<p>这个事件的处理和 "client.connected" 事件，不过需要过滤掉 client 因为用户名和密码没有通过认证，触发的 "client.disconnected"：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/src/emqx_rabbitmq_hook.erl
on_client_disconnected(#{}, auth_failure, _Env) -&gt;
  ok;

on_client_disconnected(#{client_id := ClientId, username := Username}, ReasonCode, _Env) -&gt;
  Reason = if
             is_atom(ReasonCode) -&gt;
               ReasonCode;
             true -&gt;
               unknown
           end,
  Doc = {
    client_id, ClientId,
    username, Username,
    disconnected_at, emqx_time:now_ms(),
    reason, Reason
  },
  emqx_rabbitmq_hook_cli:publish(bson_binary:put_document(Doc), &lt;&lt;"client.disconnected"&gt;&gt;),
  ok.
</code></pre>
<p>我们通过参数的模式匹配，没通过认证的 "client.disconnected" 事件会落入第一个 on_client_disconnected 函数中，不作任何处理。</p>
<h3 id="messagepublish">处理 "message.publish" 事件</h3>
<p>在处理这个事件时，需要过滤掉来自系统主题的 Publish 事件：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/src/emqx_rabbitmq_hook.erl
on_message_publish(Message = #message{topic = &lt;&lt;"$SYS/", _/binary&gt;&gt;}, _Env) -&gt;
  {ok, Message};

on_message_publish(Message = #message{topic = Topic, flags = #{retain := Retain}}, _Env) -&gt;
  Username = case maps:find(username, Message#message.headers) of
               {ok, Value} -&gt; Value;
               _ -&gt; undefined
             end,
  Doc = {
    client_id, Message#message.from,
    username, Username,
    topic, Topic,
    qos, Message#message.qos,
    retained, Retain,
    payload, {bin, bin, Message#message.payload},
    published_at, emqx_time:now_ms(Message#message.timestamp)
  },
  emqx_rabbitmq_hook_cli:publish(bson_binary:put_document(Doc), &lt;&lt;"message.publish"&gt;&gt;),
  {ok, Message}.
</code></pre>
<p>同样地，这里使用参数的模式匹配，来自于系统主题的 "message.publish" 事件会落入第一个 on_message_publish 函数中，不作任何处理。</p>
<p>这里使用 emqx_time:now_ms 函数获取到消息发布的以毫秒为单位的时间，这样可以解决之前 NTP 服务中以秒为单位而导致的计时不够精确的问题。</p>
<h3 id="">插件配置文件</h3>
<h4 id="config">.config 配置文件</h4>
<p>插件的配置文件是放在<code>emqx_rabbitmq_hook/etc/</code> 下的，默认情况下是一个 Erlang 风格的 .config 文件，这种配置文件实际上就是 Erlang 的源文件，内容是一个 Erlang 的列表，例如：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/etc/emqx_rabbitmq_hook.config
[
  {emqx_rabbitmq_hook, [{enabled, true}]}
].
</code></pre>
<p>EMQ X 在启动的时候会加载这个列表，在插件里可以通过下面的方式读取到这个列表里元素的值：</p>
<pre><code>(emqx@127.0.0.1)1&gt; application:get_env(emqx_rabbitmq_hook, enabled).
{ok,true}
</code></pre>
<p>这种风格的配置文件对 Erlang 用户来说是没什么问题的，但是对非 Erlang 的用户来说，可读性还是稍微差了一点，EMQ X 3.0 以后提供了非 Erlang 格式的 .conf 配置文件，我们在之前的课程中已经见到过了：</p>
<pre><code>xxx.xx.xx = xxx
</code></pre>
<p>这种配置文件需要配置一个映射规则，在 EMQ X 启动时通过 <a href="https://github.com/emqx/cuttlefish">cuttlefish</a> 转换成上面的 Erlang 列表。接下来我们来看怎么做。</p>
<h4 id="conf">.conf 配置文件</h4>
<h4 id="-1">映射文件</h4>
<p>以是否监听 "client.connected" 事件的配置为例，首先新增配置文件：</p>
<pre><code>### emqx_rabbitmq_hook/etc/emqx_rabbitmq_hook.conf
hook.rabbitmq.client.connected = on
</code></pre>
<p>然后，新增映射规则：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/priv/emqx_rabbitmq_hook.schema
{mapping, "hook.rabbitmq.client.connected", "emqx_rabbitmq_hook.client_connected", [
  {default, on},
  {datatype, flag}
]}.
</code></pre>
<p>映射规则文件其实也是一个 Erlang 的源文件，上面的代码将 "hook.rabbitmq.client.connected" 进行映射，并指定它的默认值和类型。</p>
<p>在 emqx-rabbitmq-hook 的 Makefile 里面指明了用 cuttlefish 对配置文件和映射文件进行处理：</p>
<pre><code>emqx_rabbitmq_hook/Makefile
app.config::
    ./deps/cuttlefish/cuttlefish -l info -e etc/ -c etc/emqx_rabbitmq_hook.conf -i priv/emqx_rabbitmq_hook.schema -d data
</code></pre>
<p>重新编译后，运行 <code>emqx-rel/_build/emqx/rel/emqx/bin/emqx console</code>，在控制台中输入<code>application:get_env(emqx_rabbitmq_hook, client_connected).</code>，就可以获取这个配置项的值：</p>
<pre><code>emqx@127.0.0.1)1&gt; application:get_env(emqx_rabbitmq_hook, client_connected).
{ok,true}
</code></pre>
<p>函数的参数 (emqx_rabbitmq_hook, client_connected) 和在映射文件里面配置的"emqx_rabbitmq_hook.client_connected"是对应的。</p>
<h4 id="-2">更复杂的映射</h4>
<p>在映射某些配置项时，还需要写一点代码，比如配置发布事件的 exchange 名时，RabbitMQ Erlang Client接受的 exchange 参数是二进制串，比如<code>&lt;&lt;mqtt.events&gt;&gt;</code>，而从 .conf 配置文件只能读取到字符串值，所以需要再做一个转化：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/priv/emqx_rabbitmq_hook.schema
{mapping, "hook.rabbitmq.exchange", "emqx_rabbitmq_hook.exchange", [
  {default, "mqtt.events"},
  {datatype, string}
]}.
{translation, "emqx_rabbitmq_hook.exchange", fun(Conf) -&gt;
      list_to_binary(cuttlefish:conf_get("hook.rabbitmq.exchange", Conf))
end}.
</code></pre>
<p>连接池 ecpool 的初始化方法接受的是一个配置项的列表，所以需要将配置文件中的 key-value 对转换成一个列表:</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/priv/emqx_rabbitmq_hook.schema
{translation, "emqx_rabbitmq_hook.server", fun(Conf) -&gt;
    Pool = cuttlefish:conf_get("hook.rabbitmq.pool", Conf),
    Host = cuttlefish:conf_get("hook.rabbitmq.host", Conf),
    Port = cuttlefish:conf_get("hook.rabbitmq.port", Conf),
    [{pool_size, Pool},
     {host, Host},
     {port, Port}
    ]
end}.
</code></pre>
<p>修改完配置映射文件后，需要重新编译。我们可以按照上面的规则继续添加更多的配置项。</p>
<h3 id="-3">使用配置项</h3>
<p>最后一步是在代码里读取这些配置项，然后根据配置项的值进行相应操作。</p>
<p><strong>初始化连接池：</strong></p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/src/emqx_rabbitmq_hook_sub.erl
init([]) -&gt;
  {ok, PoolOpts} = application:get_env(?APP, server),
  PoolSpec = ecpool:pool_spec(?APP, ?APP, emqx_rabbitmq_hook_cli, PoolOpts),
  {ok, {{one_for_one, 10, 100}, [PoolSpec]}}.
</code></pre>
<p><strong>设置 exchange:</strong></p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/src/emqx_rabbitmq_hook_cli.erl
ensure_exchange() -&gt;
  {ok, ExchangeName} = application:get_env(?APP, exchange),
  ensure_exchange(ExchangeName).

publish(Payload, RoutingKey) -&gt;
  {ok, ExchangeName} = application:get_env(?APP, exchange),
  publish(ExchangeName, Payload, RoutingKey).  
</code></pre>
<p><strong>注册 Hook:</strong>
这里首先实现一个根据配置进行 hook 注册的工具方法：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/src/emqx_rabbitmq_hook.erl
hookup(Event, ConfigName, Func, InitArgs) -&gt;
  case application:get_env(?APP, ConfigName) of
    {ok, true} -&gt; emqx:hook(Event, Func, InitArgs);
    _ -&gt; ok
  end.
</code></pre>
<p>然后在插件加载时调用：</p>
<pre><code class="erlang language-erlang">%% emqx_rabbitmq_hook/src/emqx_rabbitmq_hook.erl
load(Env) -&gt;
  ...
  hookup('client.connected', client_connected, fun ?MODULE:on_client_connected/4, [Env]),
  hookup('client.disconnected', client_disconnected, fun ?MODULE:on_client_disconnected/3, [Env]),
  hookup('message.publish', message_publish, fun ?MODULE:on_message_publish/2, [Env]).
</code></pre>
<p>重新编译之后，加载插件，修改几个配置项以后再重新加载插件，可以观察配置项是否生效。</p>
<hr />
<p>这一节我们完成了 emqx-rabbitmq-hook 的全部功能，下一节我们将在 IotHub 中使用 emqx-rabbitmq-hook。 </p></div></article>